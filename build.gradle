
// initial extensions to gradle
buildscript {
    ext {
        springBootVersion = '1.5.3.RELEASE'
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath group: 'com.cinnober.gradle', name: 'semver-git', version: '2.2.1'
        classpath 'org.owasp:dependency-check-gradle:1.4.5.1'
    }
}

// code
apply plugin: 'java'
apply plugin: 'org.springframework.boot'

// code quality
apply plugin: 'jacoco'
apply plugin: 'org.owasp.dependencycheck'

// IDEs
apply plugin: 'eclipse'
apply plugin: 'idea'

// publishing
apply plugin: 'com.cinnober.gradle.semver-git'

// XXX please change with your commits according to http://semver.org/
project.version = '0.2.0'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url "https://jitpack.io"
    }
    maven {
        url 'https://maven.man.poznan.pl/repository/kiwi-repo'
    }
}


// Spring related configs
dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:Dalston.RELEASE"
    }
}

/*
Forces dependencies' cache invalidation for each build for dependencies marked with
    { changing = true }
    e.g.
    compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT'){ changing = true }
 */
configurations.all {
    // check for updates every build
    resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
}

dependencies {

    /* SymbIoTeLibraries manual:
        One can use the library from jitpack by default with the notation:
            compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT') {changing = true }
        or having cloned locally the SymbIoTeLibraries repo use it directly with project dependency by:
            compile project(':SymbIoTeLibraries')
        Important --- In order to use the latter you need to:
            1) switch comments on the artifact and project dependencies
            2) always have only one uncommented
            3) project dependency requires changes in settings.gradle file in this project
            4) never commit build.gradle which has project dependencies active as it will break CI builds
    */
    // compile('com.github.symbiote-h2020:SymbIoTeLibraries:develop-SNAPSHOT')
    compile('com.github.symbiote-h2020:SymbIoTeLibraries:0.2.0')


    compile('org.springframework.cloud:spring-cloud-starter')
    compile('org.springframework.cloud:spring-cloud-starter-config')
    compile('org.springframework.cloud:spring-cloud-starter-eureka')
    compile('org.springframework.cloud:spring-cloud-starter-zipkin') // Causes the java.net.ConnectException
    compile('org.springframework.cloud:spring-cloud-starter-stream-rabbit')
    compile('org.springframework.boot:spring-boot-starter-data-rest')
    compile('org.springframework.boot:spring-boot-starter-web')

//    compile('com.fasterxml.jackson.core:jackson-core:2.8.5')
//    compile('com.fasterxml.jackson.core:jackson-databind:2.8.5')
//    compile('com.fasterxml.jackson.core:jackson-annotations:2.8.5')
//    compile group: 'org.springframework.data', name: 'spring-cql', version: '1.5.0.M1'

    //Jena dependencies
//    compile('org.apache.jena:apache-jena-libs:3.1.0')
    compile('org.apache.jena:jena-core:3.1.0')
    compile('org.apache.jena:jena-querybuilder:3.1.0')
    compile('org.apache.jena:jena-spatial:3.1.0') {
        exclude group: 'org.slf4j' , module: 'slf4j-log4j12'
    }

//    compile('org.semanticweb.owl:align:1.0')
//    compile('org.semanticweb.owl:proc-align:1.0')
//    compile('fr.inrialpes.exmo:ontowrap:1.0')
    compile('eu.symbiote-h2020:MediationFramework:1.0')

    testCompile('org.springframework.boot:spring-boot-starter-test')

    // Use JUnit4
    testCompile 'junit:junit:4.12'
}

// code quality below
task generateJavaDocs(type: Javadoc) {
    source = sourceSets.main.allJava
    destinationDir = reporting.file("javadocs")
}

// jacoco configuration section
jacoco {
    toolVersion = "0.7.9"
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

test {
    jvmArgs '-noverify'
}

// owasp config
dependencyCheck {
    outputDirectory = "build/reports/security"
}

// including code quality extensions into the build
check.dependsOn(jacocoTestReport, 'dependencyCheck')

// Eureka needs to be disabled for testing
tasks.withType(Test) {
    systemProperty 'eureka.client.enabled', 'false'
}